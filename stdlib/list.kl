#lang kernel

(import (shift kernel 1))
(import (shift "n-ary-app.kl" 1))
(import (shift "quasiquote.kl" 1))

(import "n-ary-app.kl")
(import "defun.kl")

(defun foldr (f base lst)
  (case lst
    [(:: x xs)
     (f x (foldr f base xs))]
    [(nil)
     base]))

(defun map (f lst)
  (foldr
    (lambda (elem accum) (:: (f elem) accum))
    (nil)
    lst))

(defun filter (predicate lst)
  (foldr
    (lambda (elem accum)
      (if (predicate elem)
        (:: elem accum)
        accum))
    (nil)
    lst))

(defun snoc (xs x)
  (case xs
    [(nil) (:: x (nil))]
    [(:: y ys) (:: y (snoc ys x))]))

(defun reverse (xs)
  (case xs
    [(nil) (nil)]
    [(:: x xs) (snoc (reverse xs) x)]))

(defun syntax->list (stx)
  (syntax-case stx
    [() (nil)]
    [(cons x xs) (:: x (syntax->list xs))]))

(defun list->syntax (xs0 stx)
  (case xs0
    [(nil)
     (empty-list-syntax stx)]
   [(:: x xs)
    (cons-list-syntax x
      (list->syntax xs stx)
      stx)]))


(define-macros
  ([list
     (lambda (stx)
       (syntax-case stx
         [(cons _ more)
          (syntax-case more
           [()
            (pure (replace-loc more '(nil)))]
           [(cons x xs)
            (pure (quasiquote/loc more (:: ,x ,(cons-list-syntax 'list xs xs))))])]))]))

(example
  (foldr + 0 (list 1 2 3 4)))

(example
  (map string-length (list "foo" "bar" "baz" "quux")))

(example
  (filter (< 10) (list 1 11 111 2 22 222 3 33 333)))

(example
  (snoc (list 1 2 3) 4))

(example
  (reverse (list 1 2 3)))

(example
  (syntax->list '(a b c)))

(example
  (list->syntax (list 'a 'b 'c) 'here))

(export List nil :: foldr map filter snoc reverse syntax->list list->syntax list)
