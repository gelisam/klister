#lang kernel

-- N-ary function abstraction and application

[import [shift kernel 1]]

(define-macros
  ([if
    (lambda (stx)
      (syntax-case stx
        [(list (_ c t f))
         (pure
          (list-syntax
           ('case c
             (list-syntax ['(true) t] t)
             (list-syntax ['(false) f] f))
           stx))]))]))

[define-macros
  -- (my-app f foo bar baz) => (((f foo) bar) baz)
  ([my-app
    [lambda [stx]
      (syntax-case stx
        [[cons _ fun-and-args]
         (syntax-case fun-and-args
           [[cons fun args]
            (syntax-case args
              [(nil) [pure fun]]
              [[cons arg0 more-args]
               [pure
                [cons-list-syntax
                  'my-app
                  [cons-list-syntax
                    [list-syntax ['#%app fun arg0] stx]
                    more-args
                    stx]
                  stx]]])])])]]
   -- (my-lam (x y z) body
   -- =>
   -- (lambda (x)
   --   (lambda (y)
   --     (lambda (z)
   --       body)))
   [my-lam
    [lambda [stx]
     (syntax-case stx
       [[list [_ args body]]
        (syntax-case args
          [(nil) [pure body]]
          [[cons arg0 more-args]
           [pure [list-syntax ['lambda
                               [list-syntax [arg0] arg0]
                               [list-syntax ['my-lam more-args body] stx]]
                           stx]]]
          [[list [arg]]
           [pure [list-syntax ['lambda args body] stx]]])])]])]

[define-macros
  -- (f foo bar baz) => (my-app foo bar baz)
  ([#%app
    [lambda [stx]
     (syntax-case stx
      [[cons _ args]
       [pure [cons-list-syntax 'my-app args stx]]])]])]

[define-macros
  -- (lambda (x y z) ...) => (my-lam (x y z) ...)
  ([lambda
    [lambda [stx]
     (syntax-case stx
      [[list [_ args body]]
       [pure [list-syntax ['my-lam args body] stx]]])]])]

(define-macros
  ([flet*
    (lambda (stx)
      (syntax-case stx
        [(list (_ fun body))
         (syntax-case fun
           [(list (name args def))
            (syntax-case args
              [(cons arg more-args)
               (pure (list-syntax
                      ('flet
                       (list-syntax (name
                                     (list-syntax (arg) stx)
                                     (list-syntax ('lambda more-args def) stx))
                                    stx)
                       body)
                      stx))])])]))]))

(define-macros
  ([flet (lambda (stx)
           (syntax-case stx
             [(cons _ rest)
              (pure (cons-list-syntax 'flet* rest stx))]))]))

(define-macros
  -- (my-arrow foo bar baz) => (-> foo (-> bar baz))
  ([my-arrow (lambda (stx)
               (syntax-case stx
                 [(cons _ args)
                  (syntax-case args
                    [(list (arg1 arg2))
                     (pure (list-syntax ('-> arg1 arg2)
                                        stx))]
                    [(cons arg1 args)
                     (pure (list-syntax ('-> arg1
                                          (cons-list-syntax 'my-arrow args stx))
                                        stx))])]))]))

(export (rename ([my-arrow ->]) my-arrow))
(export if #%app lambda flet)
